import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'java-library'
    id 'com.gradleup.shadow' version '8.3.0'
    id 'fabric-loom'
}

var archives_base_name = "bot-fabric"

repositories {
    maven { url 'https://maven.xpple.dev/maven2' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    include modImplementation("dev.xpple:clientarguments:1.9")
    implementation project(path: ":common", configuration: "namedElements")
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.fabric_version
    inputs.property "description", project.description
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.fabric_version,
                "description": project.description
    }
}

shadowJar {
    archiveFileName = "bot-fabric-${project.version}-dev.jar"

    dependencies {
        exclude('net.fabricmc:.*')
        include(dependency("blade:.*"))
        exclude '/mappings/*'
    }
}

task remappedShadowJar(type: RemapJarTask) {
    dependsOn tasks.shadowJar
    input = tasks.shadowJar.archiveFile
    addNestedDependencies = true
    archiveFileName = "bot-fabric-${project.version}.jar"
}

tasks.assemble.dependsOn tasks.remappedShadowJar

artifacts {
    archives remappedShadowJar
    shadow shadowJar
}

java {
    archivesBaseName = archives_base_name
}

base {
    archivesName = archives_base_name
}
