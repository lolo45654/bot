plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.papermc.paperweight.userdev' version '1.7.1'
}

var archives_base_name = "bot-paper"
paperweight.reobfArtifactConfiguration = io.papermc.paperweight.userdev.ReobfArtifactConfiguration.Companion.REOBF_PRODUCTION

repositories {
    mavenCentral()
    // PaperMC
    maven { url "https://repo.papermc.io/repository/maven-public/" }
    // Command API via maven central snapshots
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    // InvUI
    maven { url "https://repo.xenondevs.xyz/releases" }
}

dependencies {
    paperweight.paperDevBundle(project.paper_version)

    implementation "dev.jorel:commandapi-bukkit-shade:9.5.1"
    implementation "xyz.xenondevs.invui:invui:1.32"
    implementation project(path: ":common", configuration: "namedElements")
    implementation 'org.bstats:bstats-bukkit:3.0.2'
}

shadowJar {
    // archiveFileName = "${archives_base_name}.jar"

    relocate("xyz.xenondevs.invui", "blade.libs.invui")
    relocate("dev.jorel.commandapi", "blade.libs.commandapi")
    relocate("org.bstats", "blade.bstats")
}

assemble {
    dependsOn(reobfJar)
}

artifacts {
    archives shadowJar
}

java {
    archivesBaseName = archives_base_name
}

base {
    archivesName = archives_base_name
}

processResources {
    def props = [version: project.version, description: project.description]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}